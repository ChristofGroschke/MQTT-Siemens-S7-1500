FUNCTION_BLOCK "MQTT"
TITLE = 'MQTT MAIN Function(PN)'
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : Maul
VERSION : 1.0
   VAR_INPUT 
      connectionID : Int := 1;   // S7 Connection ID for CP Ethernet
      ipBlock1 : Int := 0;
      ipBlock2 : Int := 0;
      ipBlock3 : Int := 0;
      ipBlock4 : Int := 0;
      ipPort : Int := 0;
      ipPort_Byte { S7_HMI_Accessible := 'False'; S7_HMI_Visible := 'False'} AT ipPort : Struct
         byte1 { S7_HMI_Accessible := 'False'; S7_HMI_Visible := 'False'} : Byte;
         byte2 { S7_HMI_Accessible := 'False'; S7_HMI_Visible := 'False'} : Byte;
      END_STRUCT;
   END_VAR

   VAR_OUTPUT 
      pubTopic : String[254];
      pubTopicChar { S7_HMI_Accessible := 'False'; S7_HMI_Visible := 'False'} AT pubTopic : Array[0..255] of Char;
      payload : Array[0..4096] of Byte;   // Must match mqttGlobals.TCP_MAXRECVSIZE
      payloadSize : Int;
   END_VAR

   VAR_IN_OUT 
      callback : Bool;
      callbackPacketType : Int;
   END_VAR

   VAR 
      tcpState : Int := 0;   // TCP state: TCP_UNINITIALIZED MQTT Protocol State Control
      intState : Int;   // State Machine
      lastState : Int;   // State Machine
      tcpRecByte : Byte;   // Next incoming TCP byte
      myPacketReader : "mqttPacketReader";   // MQTT PacketReader Instance
      msgId : Int;
      typeOfPacket : Int;
      Runtimer : "mqttRuntime";
      TCP_Timer : "mqttRuntime";
      resetTime : Time;
      tl : Int;
      paylPos : Int;
      paylSize : Int;   // debug
      recBufLeft : Int;   // Send TCP State
      act_send : Bool;
      snd_busy : Bool;
      last_snd_err : Word;   // Receive TCP State
      last_rec_err : Word;   // Control TCP State
      cntrl_done : Bool;
      cntrl_error : Bool;
      cntrl_status : Word;
      cntrl_lastErrStatus : Word;
      cntrl_res1 : DWord;
      cntrl_res2 : DWord;
      netcmd_success : Bool := FALSE;
      netcmd_busy : Bool := FALSE;
      netcmd_error : Bool := FALSE;
      netcmd_status : Word;
      netcmd_datareceived : Bool := FALSE;
      netcmd_datalength : Int;
      NET_CON {OriginalPartName := 'T__CON'; LibVersion := '4.0'} : TCON;
      NET_DISCON {OriginalPartName := 'T_DISCON'; LibVersion := '2.1'} : TDISCON;
      NET_SEND {OriginalPartName := 'T__SEND'; LibVersion := '4.0'} : TSEND;
      NET_RCV {OriginalPartName := 'T__RCV'; LibVersion := '4.0'} : TRCV;
      NET_CONFIG {OriginalPartName := 'TCON_IP_v4'; LibVersion := '1.0'} : TCON_IP_v4;
   END_VAR

   VAR_TEMP 
      i : Int;
      intTemp : Int;
      TOP_SI : Struct
         EV_CLASS : Byte;
         EV_NUM : Byte;
         PRIORITY : Byte;
         NUM : Byte;
         TYP2_3 : Byte;
         TYP1 : Byte;
         ZI1 : Word;
         ZI2_3 : DWord;
      END_STRUCT;
      START_UP_SI {OriginalPartName := 'SI_classic'; LibVersion := '1.0'} : SI_classic;
      tmpPacketID : Int;
      structTmpPacketId AT tmpPacketID : Struct
         byteLSB : Byte;
         byteMSB : Byte;   // TCP State Control
      END_STRUCT;
   END_VAR

   VAR CONSTANT 
      TCP_STATE_UNINITIALIZED : Int := 0;
      TCP_STATE_INITIALIZING : Int := 1;
      TCP_STATE_INITIALIZED : Int := 2;
      TCP_STATE_CONNECTING : Int := 3;
      TCP_STATE_CONNECTED : Int := 4;
      TCP_STATE_DISCONNECTING : Int := 5;
      TCP_STATE_DISCONNECTED : Int := 6;   // MQTT States
      MQTT_STATE_DISCONNECT : Int := -6;
      MQTT_STATE_DISCONNECTING : Int := -5;
      MQTT_STATE_DISCONNECTED : Int := -1;
      MQTT_STATE_CONNECTING : Int := 0;
      MQTT_STATE_CONNECTED : Int := 1;   // packet reading States
      WAIT_FOR_RECEIVED_PACKET : Int := 0;
      READ_COMPLETE_PACKET : Int := 1;   // MQTT Message Types
      MQTT_CONNECT : Int := 16;
      MQTT_CONNACK : Int := 32;
      MQTT_PUBLISH : Int := 48;
      MQTT_PUBACK : Int := 64;
      MQTT_PUBREC : Int := 80;
      MQTT_PUBREL : Int := 96;
      MQTT_PUBCOMP : Int := 112;
      MQTT_SUBSCRIBE : Int := 130;
      MQTT_SUBACK : Int := 144;
      MQTT_UNSUBSCRIBE : Int := 160;
      MQTT_UNSUBACK : Int := 176;
      MQTT_PINGREQ : Int := 192;
      MQTT_PINGRESP : Int := 208;
      MQTT_DISCONNECT : Int := 224;   // Types of Ethernet Adapters
      PN : Int := 0;
      CP : Int := 1;   // CP AG_CTRL COMMANDS
      CN_NOP : Int := 0;   // do nothing
      CN_STATUS : Int := 1;
      CN_RESET : Int := 2;
      CN_STATUS_ALL : Int := 3;   // get status for ALL connections
      CN_RESET_ALL : Int := 4;   // reset ALL connections
      CN_CLEAR_RESET : Int := 5;
      CN_DISCON : Int := 6;   // disconnect the connection, no automatic reconnect
      CN_STARTCON : Int := 7;   // initiate connection
   END_VAR


BEGIN
	
	(* Check IF CPU had a reset, trigger network reinitialization on reset *)
	#intTemp := RD_SINFO(TOP_SI => #TOP_SI, START_UP_SI => #START_UP_SI);
	IF #START_UP_SI.EV_NUM <> 3 THEN
	  #tcpState := #TCP_STATE_UNINITIALIZED;
	  "mqttData".ethTCPConnected := false;
	  "mqttData"._state := #MQTT_STATE_DISCONNECTED;
	END_IF;
	
	
	(* 
	    ******************************
	    TCP/IP State Machine
	    ******************************
	*)
	CASE (#tcpState) OF
	    
	  #TCP_STATE_UNINITIALIZED:
	    "mqttData".ethTCPConnected := false;
	    #tcpState := #TCP_STATE_INITIALIZING;
	    ;
	    
	  #TCP_STATE_INITIALIZING:
	    
	    // *** PN CODE ****
	    
	    // Load IP parameters to NET_CONFIG DB variables for usage in TCON
	    #NET_CONFIG.RemoteAddress.ADDR[1] := INT_TO_BYTE(#ipBlock1);
	    #NET_CONFIG.RemoteAddress.ADDR[2] := INT_TO_BYTE(#ipBlock2);
	    #NET_CONFIG.RemoteAddress.ADDR[3] := INT_TO_BYTE(#ipBlock3);
	    #NET_CONFIG.RemoteAddress.ADDR[4] := INT_TO_BYTE(#ipBlock4);
	    #NET_CONFIG.RemotePort := BYTE_TO_INT(#ipPort_Byte.byte2) OR SHL(IN := BYTE_TO_INT(#ipPort_Byte.byte1), N := 8);
	    
	    
	    #NET_CON(REQ := false // IN: BOOL
	             ,
	             ID := INT_TO_WORD(#connectionID) // IN: WORD
	             ,
	             CONNECT := #NET_CONFIG // INOUT: ANY
	    );
	    
	    #NET_SEND(REQ := false // IN: BOOL
	              ,
	              ID := INT_TO_WORD(#connectionID) // IN: WORD
	              ,
	              LEN := 0// IN: INT
	              ,
	              DATA := "mqttData".tcpSendBuf // INOUT: ANY
	    );
	    
	    #NET_RCV(EN_R := false // IN: BOOL
	             ,
	             ID := INT_TO_WORD(#connectionID) // IN: WORD
	             ,
	             LEN := 0 // IN: INT
	             ,
	             DATA := #tcpRecByte // INOUT: ANY
	    );
	    
	    #NET_DISCON(REQ := false // IN: BOOL
	                ,
	                ID := INT_TO_WORD(#connectionID) // IN: WORD
	    );
	    
	    // *** PN CODE END ****
	    
	    #tcpState := #TCP_STATE_INITIALIZED;
	    ;
	    
	  #TCP_STATE_INITIALIZED:
	    #tcpState := #TCP_STATE_CONNECTING;
	    ;
	    
	  #TCP_STATE_CONNECTING:
	    
	    // *** PN CODE ****
	    
	    #NET_CON(REQ := true // IN: BOOL
	             ,
	             ID := INT_TO_WORD(#connectionID) // IN: WORD
	             ,
	             CONNECT := #NET_CONFIG // INOUT: ANY
	    );
	    #netcmd_success := #NET_CON.DONE; // OUT: BOOL
	    #netcmd_error := #NET_CON.ERROR; // OUT: BOOL
	    #netcmd_status := #NET_CON.STATUS; // OUT: WORD
	    
	    IF #netcmd_success AND (#netcmd_status = W#16#0) THEN
	      #tcpState := #TCP_STATE_CONNECTED;
	      "mqttData".ethTCPConnected := true;
	    END_IF;
	    IF #netcmd_error THEN
	      #cntrl_lastErrStatus := #netcmd_status;
	      #tcpState := #TCP_STATE_DISCONNECTED;
	    END_IF;
	    
	    // TCP timeout while connecting reset connection state and try again
	    
	    IF NOT #netcmd_success THEN
	      #TCP_Timer(IN := true,
	                 RES := false);
	      
	      IF TIME_TO_DINT(#TCP_Timer.RTIME) > TIME_TO_DINT("mqttGlobals".TCP_TIMEOUT) THEN
	        #tcpState := #TCP_STATE_DISCONNECTING;
	      END_IF;
	    END_IF;
	    
	    // *** PN CODE END ****
	    ;
	    
	  #TCP_STATE_CONNECTED:
	    
	    // *** PN CODE - Check TCP connection state ****
	    
	    #NET_RCV(EN_R := false // IN: BOOL
	             ,
	             ID := INT_TO_WORD(#connectionID) // IN: WORD
	             ,
	             LEN := 0 // IN: INT
	             ,
	             DATA := #tcpRecByte // INOUT: ANY
	    );
	    IF #NET_RCV.STATUS <> W#16#7000 THEN
	      #tcpState := #TCP_STATE_DISCONNECTING;
	    END_IF;
	    ;
	    
	  #TCP_STATE_DISCONNECTING:
	    "mqttData".ethTCPConnected := false;
	    
	    // *** PN CODE ****
	    #NET_DISCON(REQ := true // IN: BOOL
	                ,
	                ID := INT_TO_WORD(#connectionID) // IN: WORD
	    );
	    #netcmd_success := #NET_DISCON.DONE; // OUT: BOOL
	    #netcmd_busy := #NET_DISCON.BUSY; // OUT: BOOL
	    #netcmd_error := #NET_DISCON.ERROR; // OUT: BOOL
	    #netcmd_status := #NET_DISCON.STATUS; // OUT: WORD
	    
	    #tcpState := #TCP_STATE_DISCONNECTED;
	    
	    // *** PN CODE END ****
	    ;
	    
	  #TCP_STATE_DISCONNECTED:
	    "mqttData".ethTCPConnected := false;
	    #tcpState := #TCP_STATE_UNINITIALIZED;
	    #TCP_Timer(IN := false,
	               RES := true);
	    ;
	    
	  ELSE:
	    ;
	END_CASE;
	
	(*
	    *******************************************
	    MQTT Timout Handling without TCP Connection
	    *******************************************
	*)
	#Runtimer(IN := (("mqttData"._state = #MQTT_STATE_CONNECTING) OR ("mqttData"._state = #MQTT_STATE_CONNECTED)),
	          RES := false);
	"mqttData".runTime := #Runtimer.RTIME;
	
	//Recalculate timeouts when timer is set to T#0
	
	IF TIME_TO_DINT("mqttData".runTime) = 0 AND (TIME_TO_DINT("mqttData".lastInActivity) <> 0 OR TIME_TO_DINT("mqttData".lastOutActivity) <> 0) THEN
	  #Runtimer.run := TIME_TO_DINT("mqttGlobals".MQTT_KEEPALIVE) / 1000;
	  
	  IF TIME_TO_DINT("mqttData".lastOutActivity) <> 0 THEN
	    "mqttData".lastOutActivity := "mqttGlobals".MQTT_KEEPALIVE - DINT_TO_TIME(2073600000 - TIME_TO_DINT("mqttData".lastOutActivity));
	  END_IF;
	  
	  IF TIME_TO_DINT("mqttData".lastInActivity) <> 0 THEN
	    "mqttData".lastInActivity := "mqttGlobals".MQTT_KEEPALIVE - DINT_TO_TIME(2073600000 - TIME_TO_DINT("mqttData".lastInActivity));
	  END_IF;
	END_IF;
	
	IF ("mqttData"._state = #MQTT_STATE_CONNECTING OR "mqttData"._state = #MQTT_STATE_CONNECTED) AND #tcpState <> #TCP_STATE_CONNECTED THEN
	  IF (("mqttData".runTime - "mqttData".lastInActivity) > "mqttGlobals".MQTT_KEEPALIVE) OR (("mqttData".runTime - "mqttData".lastOutActivity) > "mqttGlobals".MQTT_KEEPALIVE) THEN
	    // Connection Timeout!
	    "mqttData"._state := #MQTT_STATE_DISCONNECTED;
	    "mqttData".reqSend := false;
	    #act_send := false;
	    #snd_busy := false;
	  END_IF;
	END_IF;
	
	(* 
	    ******************************
	    TCP/IP Sending and Receiving
	    ******************************
	*)
	IF NOT (#tcpState = #TCP_STATE_CONNECTED) THEN
	  RETURN;
	END_IF;
	
	(* TCP SENDING *)
	IF ("mqttData".reqSend AND NOT #snd_busy) THEN
	  "mqttData".reqSend := false;
	  #act_send := true;
	END_IF;
	
	
	// *** PN CODE ****
	#NET_SEND(REQ := #act_send // IN: BOOL
	          ,
	          ID := INT_TO_WORD(#connectionID) // IN: WORD
	          ,
	          LEN := "mqttData".sendSize // IN: INT
	          ,
	          DATA := "mqttData".tcpSendBuf // INOUT: ANY
	);
	#netcmd_success := #NET_SEND.DONE; // OUT: BOOL
	#netcmd_error := #NET_SEND.ERROR; // OUT: BOOL
	#netcmd_status := #NET_SEND.STATUS; // OUT: WORD
	
	// after sending reset send buffer management
	"mqttData".sendSize := 0;
	"mqttData".tcp_sendBufferFull := false;
	
	IF #netcmd_error AND NOT #netcmd_success THEN
	  #last_snd_err := #netcmd_status;
	  #snd_busy := false;
	  
	  IF ((#netcmd_status = W#16#80A1) OR (#netcmd_status = W#16#80C4)) THEN
	    "mqttData".ethTCPConnected := false;
	    "mqttData"._state := #MQTT_STATE_DISCONNECTED;
	    #tcpState := #TCP_STATE_DISCONNECTING;
	  END_IF;
	END_IF;
	// *** PN CODE END ****
	
	
	IF (#act_send) THEN
	  #act_send := false;
	  #snd_busy := true;
	END_IF;
	
	IF (#netcmd_success AND #snd_busy) THEN
	  #snd_busy := false;
	END_IF;
	
	
	(* TCP RECEIVING *)
	
	// *** PN CODE ****
	#NET_RCV(EN_R := true // IN: BOOL
	         ,
	         ID := INT_TO_WORD(#connectionID) // IN: WORD
	         ,
	         LEN := #netcmd_datalength // IN: INT
	         ,
	         DATA := #tcpRecByte // INOUT: ANY
	);
	#netcmd_datareceived := #NET_RCV.NDR; // OUT: BOOL
	#netcmd_error := #NET_RCV.ERROR; // OUT: BOOL
	#netcmd_status := #NET_RCV.STATUS; // OUT: WORD
	#netcmd_datalength := #NET_RCV.RCVD_LEN; // OUT: INT
	
	IF #netcmd_error AND ((#netcmd_status = W#16#80A1) OR (#netcmd_status = W#16#80C4)) THEN
	  #last_rec_err := #netcmd_status;
	  "mqttData"._state := #MQTT_STATE_DISCONNECTED;
	  #tcpState := #TCP_STATE_DISCONNECTING;
	END_IF;
	// *** PN CODE END ****
	
	
	IF #netcmd_datareceived THEN
	  IF "mqttData".RecBufFull THEN
	    "RecBufFull" := true;
	  ELSE
	    "mqttData".tcpRecBuf["mqttData".pRec] := #tcpRecByte;
	    "mqttData".pRec := "mqttData".pRec + 1;
	    IF ("mqttData".pRec >= "mqttGlobals".TCP_RECVBUFFERSIZE) THEN
	      "mqttData".pRec := 0;
	    END_IF;
	    IF ("mqttData".pRec = "mqttData".pRead) THEN
	      "mqttData".RecBufFull := true;
	    END_IF;
	  END_IF;
	END_IF;
	
	#myPacketReader();
	
	//debug
	IF ("mqttData".pRec >= "mqttData".pRead) THEN
	  #recBufLeft := "mqttGlobals".TCP_RECVBUFFERSIZE - ("mqttData".pRec - "mqttData".pRead);
	ELSE
	  #recBufLeft := "mqttGlobals".TCP_RECVBUFFERSIZE - ("mqttData".pRead - "mqttData".pRec);
	END_IF;
	
	
	(*
	   ******************************
	   MQTT State Machine
	   ******************************
	*)
	CASE ("mqttData"._state) OF
	    
	  #MQTT_STATE_DISCONNECTED:
	    #lastState := "mqttData"._state;
	    // TODO: what is this for?
	    IF (("mqttData".runTime - t#10s) > #resetTime) AND NOT "mqttData".ethTCPConnected THEN
	      #resetTime := "mqttData".runTime;
	    END_IF;
	    ;
	    
	  #MQTT_STATE_CONNECTING:  // this state is triggered by mqttConnect FC
	    IF ("mqttData"._state <> #lastState) THEN
	      #intState := #WAIT_FOR_RECEIVED_PACKET;
	      #lastState := "mqttData"._state;
	    END_IF;
	    
	    CASE (#intState) OF
	        
	      #WAIT_FOR_RECEIVED_PACKET:
	        IF (("mqttData".runTime - "mqttData".lastInActivity) >= "mqttGlobals".MQTT_SOCKET_TIMEOUT) THEN
	          // Timeout!
	          "mqttData"._state := #MQTT_STATE_DISCONNECTED;
	        ELSIF "mqttAvailable"() THEN
	          #myPacketReader.iBegin := true;
	          #intState := #READ_COMPLETE_PACKET;
	        END_IF;
	        ;
	        
	      #READ_COMPLETE_PACKET:
	        IF (#myPacketReader.xDone) THEN
	          
	          #typeOfPacket := BYTE_TO_INT("mqttData".buffer[0] AND B#16#F0);
	          
	          IF (#typeOfPacket = "mqttGlobals".MQTTCONNACK) THEN
	            "mqttData".lastInActivity := "mqttData".runTime;
	            "mqttData".pingOutstanding := false;
	            
	            IF BYTE_TO_INT("mqttData".buffer[3]) = "mqttGlobals".MQTT_CONN_ACCEPTED THEN
	              "mqttData"._state := #MQTT_STATE_CONNECTED;
	            ELSE
	              // MQTT Connection Error!
	              "mqttData"._state := #MQTT_STATE_DISCONNECTED;
	              #callbackPacketType := #MQTT_CONNACK;
	              "mqttData".mqttErrorCode := BYTE_TO_INT("mqttData".buffer[3]);
	              #callback := true;
	            END_IF;
	            // ELSE maybe need equivalent/workaround to client->stop()
	          END_IF;
	        END_IF;
	        ;
	        
	    END_CASE;
	    ;
	    
	  #MQTT_STATE_CONNECTED:
	    IF ("mqttData"._state <> #lastState) THEN
	      #intState := #WAIT_FOR_RECEIVED_PACKET;
	      #lastState := "mqttData"._state;
	    END_IF;
	    // Check if Keepalive timeout occured.
	    IF (("mqttData".runTime - "mqttData".lastInActivity) > "mqttGlobals".MQTT_KEEPALIVE) OR (("mqttData".runTime - "mqttData".lastOutActivity) > "mqttGlobals".MQTT_KEEPALIVE) THEN
	      IF ("mqttData".pingOutstanding) THEN
	        // Ping Timeout!
	        "mqttData"._state := #MQTT_STATE_DISCONNECTED;
	      ELSE
	        "mqttData".buffer[0] := INT_TO_BYTE("mqttGlobals".MQTTPINGREQ);
	        "mqttData".buffer[1] := 0;
	        "mqttSendTCP"(offset := 0,
	                      size := 2);
	        "mqttData".lastOutActivity := "mqttData".runTime;
	        "mqttData".lastInActivity := "mqttData".lastOutActivity;
	        "mqttData".pingOutstanding := true;
	      END_IF;
	    END_IF;
	    
	    CASE (#intState) OF
	        
	      #WAIT_FOR_RECEIVED_PACKET:
	        IF "mqttAvailable"() THEN
	          #myPacketReader.iBegin := true;
	          #msgId := 0;
	          #intState := #READ_COMPLETE_PACKET;
	        END_IF;
	        ;
	        
	      #READ_COMPLETE_PACKET:
	        IF (#myPacketReader.xDone) THEN
	          
	          // check if received message is > 0 bytes for variable part
	          IF (#myPacketReader.result > 0) THEN
	            
	            "mqttData".lastInActivity := "mqttData".runTime;
	            
	            #typeOfPacket := BYTE_TO_INT("mqttData".buffer[0] AND B#16#F0);
	            
	            CASE (#typeOfPacket) OF
	                
	              #MQTT_PUBLISH:
	                // received a PUBLISH, 3.3 PUBLISH – Publish message
	                // refer to MQTT Docs. section 3.3 PUBLISH – Publish message
	                
	                #tl := WORD_TO_INT(SHL(IN := BYTE_TO_WORD("mqttData".buffer[#myPacketReader.lengthLength + 1]), N := 8)) + BYTE_TO_INT("mqttData".buffer[#myPacketReader.lengthLength + 2]);
	                FOR #i := 0 TO #tl - 1 DO
	                  #pubTopicChar[#i + 2] := BYTE_TO_CHAR("mqttData".buffer[#myPacketReader.lengthLength + 3 + #i]);
	                END_FOR;
	                #pubTopicChar[1] := INT_TO_CHAR(#tl);
	                #paylPos := #myPacketReader.lengthLength + 3 + #tl;
	                #paylSize := #myPacketReader.result - #paylPos;
	                FOR #i := 0 TO #paylSize - 1 DO
	                  #payload[#i] := "mqttData".buffer[#paylPos + #i];
	                END_FOR;
	                #payloadSize := #paylSize;
	                #callback := true;
	                
	                // Check if the Publish messag has QoS = 1, if yes then send PUBACK response
	                IF BYTE_TO_INT("mqttData".buffer[0] AND B#16#01) = 1 THEN
	                  "mqttPUBACK"(packetIdMSB := "mqttData".buffer[#paylPos - 2],
	                               packetIdLSB := "mqttData".buffer[#paylPos - 1]);
	                END_IF;
	                ;
	                
	              #MQTT_PINGRESP:
	                // received a PINGRESP, part of MQTT 3.1.2.10 Keep Alive
	                // (refer MQTT Docs. section 3.1.2.10 Keep Alive, 3.12 PINGREQ – PING request, 3.13 PINGRESP – PING response)
	                
	                "mqttData".pingOutstanding := false;
	                ;
	                
	              #MQTT_PUBACK:
	                // received a PUBACK package, part of MQTT QoS 1 handshake
	                // (refer MQTT Docs. section 4.3.2 QoS 1: AT least once delivery)
	                
	                #callbackPacketType := "mqttGlobals".MQTTPUBACK;
	                // get the packet ID of the PubAck package and store it in mqttData.recPublishPacketID
	                #structTmpPacketId.byteMSB := "mqttData".buffer[3];
	                #structTmpPacketId.byteLSB := "mqttData".buffer[2];
	                "mqttData".recPublishPacketID := #tmpPacketID;
	                #callback := true;
	                ;
	                
	              #MQTT_PUBREC:
	                // received a PUBREC package, part of MQTT QoS 2 handshake
	                // (refer MQTT Docs. section 4.3.3 QoS 2: Exactly once delivery)
	                
	                // get the packet ID of the PubAck package and store it in mqttData.recPublishPacketID
	                #structTmpPacketId.byteMSB := "mqttData".buffer[3];
	                #structTmpPacketId.byteLSB := "mqttData".buffer[2];
	                // send out a PUBREL packet
	                "mqttData".buffer[0] := INT_TO_BYTE("mqttGlobals".MQTTPUBREL);
	                "mqttData".buffer[0] := "mqttData".buffer[0] OR SHL(IN := INT_TO_BYTE("mqttGlobals".MQTTQOS1), N := 1); // set qos flag to 2
	                "mqttData".buffer[1] := 16#02;
	                "mqttSendTCP"(offset := 0,
	                              size := 4);
	                "mqttData".lastOutActivity := "mqttData".runTime;
	                ;
	                
	              #MQTT_PUBCOMP:
	                // received a PUBCOMP package, part of MQTT QoS 2 handshake
	                // (see MQTT Docs. section 4.3.3 QoS 2: Exactly once delivery)
	                
	                // notify about successfull QoS 2 handshake
	                #callbackPacketType := "mqttGlobals".MQTTPUBCOMP;
	                // get the packet ID of the PubAck package and store it in mqttData.recPublishPacketID
	                #structTmpPacketId.byteMSB := "mqttData".buffer[3];
	                #structTmpPacketId.byteLSB := "mqttData".buffer[2];
	                "mqttData".recPublishPacketID := #tmpPacketID;
	                #callback := true;
	                ;
	                
	            END_CASE; // typeOfPacket
	            
	          END_IF; // myPacketReader.result > 0
	          
	          #intState := #WAIT_FOR_RECEIVED_PACKET;
	          
	        END_IF; // myPacketReader.xDone
	        ;
	        
	    END_CASE; //intState
	    
	END_CASE; // mqttData._state
	
	
END_FUNCTION_BLOCK

